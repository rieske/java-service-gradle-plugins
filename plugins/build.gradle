plugins {
    id("groovy-gradle-plugin")
    id("com.gradle.plugin-publish").version("1.2.2")
}

group = "io.github.rieske.java-service-gradle-plugins"
version = "1.0.2"

tasks.named("test") {
    useJUnitPlatform()
    inputs.property("TEST_GRADLE_VERSION", System.getenv("TEST_GRADLE_VERSION")).optional(true)
}

dependencies {
    implementation("com.bmuschko:gradle-docker-plugin:9.3.2")

    testImplementation(platform("org.spockframework:spock-bom:2.3-groovy-3.0"))
    testImplementation("org.spockframework:spock-core")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

gradlePlugin {
    website = "https://github.com/rieske/java-service-gradle-plugins"
    vcsUrl = "https://github.com/rieske/java-service-gradle-plugins"
    afterEvaluate {
        plugins {
            def javaServicePlugin = it.findByName("io.github.rieske.java-service")
            javaServicePlugin.displayName = "Gradle plugin for packaging and testing Java services"
            javaServicePlugin.description = "Gradle plugin that packages your application into a Docker container and configures blackBoxTest task and source set"
            javaServicePlugin.tags.set(["java", "service", "black-box-testing", "docker"])
        }
    }
}
