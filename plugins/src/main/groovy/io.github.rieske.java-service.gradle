plugins {
    id("application")
    id("com.bmuschko.docker-remote-api")
}

tasks.register("prepareDockerContext", Copy) {
    into(layout.buildDirectory.dir("docker"))
    from(tasks.named("distTar"))
    from("Dockerfile")
}

tasks.register("docker", com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
    dependsOn("prepareDockerContext")
    inputDir.set(layout.buildDirectory.dir("docker"))
    images.add("${rootProject.name}:snapshot")
}

testing.suites {
    test {
        useJUnitJupiter()
    }
    blackBoxTest(JvmTestSuite) {
        targets {
            all {
                testTask.configure {
                    mustRunAfter(test)
                    dependsOn(tasks.named("docker"))
                    inputs.files(file("Dockerfile"), tasks.named("distTar")).withPathSensitivity(PathSensitivity.RELATIVE)
                }
            }
        }
    }
}

tasks.withType(Test).configureEach {
    testLogging.exceptionFormat = "full"
}

// Make the build reproducible - this enables caching and reusing the artifacts
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.named("check") {
    it.dependsOn(testing.suites.blackBoxTest)
}

tasks.named("build") {
    it.dependsOn(tasks.named("docker"))
}
