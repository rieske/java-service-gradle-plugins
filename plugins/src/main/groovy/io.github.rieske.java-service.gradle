plugins {
    id("application")
}

def dockerfile = layout.projectDirectory.file("Dockerfile")
def applicationTarArchive = tasks.named("distTar").map(AbstractArchiveTask::getArchiveFile)
def dockerContextDir = layout.buildDirectory.dir("docker")
def prepareDockerContextTask = tasks.register("prepareDockerContext", Copy) {
    into(dockerContextDir)
    from(applicationTarArchive)
    from(dockerfile)
}

def dockerTask = tasks.register("docker", Exec) {
    dependsOn(prepareDockerContextTask)
    inputs.files(dockerfile, applicationTarArchive).withPathSensitivity(PathSensitivity.RELATIVE)
    workingDir = dockerContextDir
    commandLine = ["docker", "build", "-t", "${rootProject.name}:snapshot", "."]
    outputs.dir(dockerContextDir)
}

testing.suites {
    test {
        useJUnitJupiter()
    }
    blackBoxTest(JvmTestSuite) {
        targets {
            all {
                testTask.configure {
                    mustRunAfter(test)
                    dependsOn(dockerTask)
                    inputs.files(dockerfile, applicationTarArchive).withPathSensitivity(PathSensitivity.RELATIVE)
                }
            }
        }
    }
}

tasks.withType(Test).configureEach {
    testLogging.exceptionFormat = "full"
}

// Make the build reproducible - this enables caching and reusing the artifacts
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.named("check") {
    it.dependsOn(testing.suites.blackBoxTest)
}

tasks.named("build") {
    it.dependsOn(dockerTask)
}
